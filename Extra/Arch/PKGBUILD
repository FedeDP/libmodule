# Maintainer: Federico Di Pierro <nierro92@gmail.com>

pkgname=libmodule-git
_gitname=libmodule
pkgver=r36.5372354
pkgrel=1
pkgdesc="C linux library to build simple and modular projects"
arch=(x86_64)
url="https://github.com/FedeDP/${_gitname}"
license=(GPL)
depends=()
makedepends=(git cmake)
source=("git://github.com/FedeDP/${_gitname}.git")
sha256sums=("SKIP")

pkgver() {
    cd "$_gitname"
    ( set -o pipefail
        git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

prepare() {
    cd "$_gitname"
    mkdir -p build
}

build() {
    cd "$_gitname"/build
    cmake \
        -G "Unix Makefiles" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE="Release" \
        ../
    make
}

package() {
    cd "$_gitname"/build
    make DESTDIR="$pkgdir" install
}
